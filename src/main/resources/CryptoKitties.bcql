SET BLOCKCHAIN "Ethereum";

SET OUTPUT FOLDER "./test_output";
SET CONNECTION "ws://localhost:8546/";

int[] kitties = newIntArray();
BLOCKS (6605100) (6615100) {
    LOG ENTRIES (0x06012c8cf97BEaD5deAe237070F9587f8E7A266d) (Birth(address owner, uint256 kittyId, uint256 matronId, uint256 sireId, uint256 genes)) {
        add(kitties, kittyId);
        EMIT XES EVENT ()(kittyId)()("birth" as xs:string concept:name);

        bool containsMatron = contains(kitties, matronId);
        IF (containsMatron) {
            EMIT XES EVENT ()(matronId)()("became mother" as xs:string concept:name);
        }

        bool containsSire = contains(kitties, sireId);
        IF (containsSire) {
            EMIT XES EVENT ()(sireId)()("became father" as xs:string concept:name);
        }
    }

    LOG ENTRIES (0x06012c8cf97BEaD5deAe237070F9587f8E7A266d) (Transfer(address from, address to, uint256 tokenId)) {
        bool containsKitty = contains(kitties, tokenId);
        IF (containsKitty) {
            EMIT XES EVENT ()(tokenId)()("transferred" as xs:string concept:name);
        }
    }

    LOG ENTRIES (0x06012c8cf97BEaD5deAe237070F9587f8E7A266d) (Pregnant(address owner, uint256 matronId, uint256 sireId, uint256 cooldownEndBlock)) {
        bool containsMatron = contains(kitties, matronId);
        IF (containsMatron) {
            EMIT XES EVENT ()(matronId)()("conceived as mother" as xs:string concept:name);
        }

        bool containsSire = contains(kitties, sireId);
        IF (containsSire) {
            EMIT XES EVENT ()(sireId)()("conceived as father" as xs:string concept:name);
        }
    }

    LOG ENTRIES (0xb1690c08e213a35ed9bab7b318de14420fb57d8c) (AuctionCreated(uint256 tokenId, uint256 startingPrice, uint256 endingPrice, uint256 duration)) {
        bool containsKitty = contains(kitties, tokenId);
        IF (containsKitty) {
            EMIT XES EVENT ()(tokenId)()("put up for auction" as xs:string concept:name);
        }
    }

    LOG ENTRIES (0xb1690c08e213a35ed9bab7b318de14420fb57d8c) (AuctionSuccessful(uint256 tokenId, uint256 totalPrice, address winner)) {
        bool containsKitty = contains(kitties, tokenId);
        IF (containsKitty) {
            EMIT XES EVENT ()(tokenId)()("auctioned" as xs:string concept:name);
        }
    }

    LOG ENTRIES (0xb1690c08e213a35ed9bab7b318de14420fb57d8c) (AuctionCancelled(uint256 tokenId)) {
        bool containsKitty = contains(kitties, tokenId);
        IF (containsKitty) {
            EMIT XES EVENT ()(tokenId)()("not auctioned" as xs:string concept:name);
        }
    }
}
